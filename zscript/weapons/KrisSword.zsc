class KrisSword : DeltaWeapon
{
	bool pulledOutFirstTime;
	int swingCombo;
	
	Default
	{
		Tag "$TAG_DELTA_SWORD";
		Obituary "$OBIT_DELTA_SWORD";
		Inventory.RestrictedTo "KrisPlayer";
		Inventory.PickupMessage "$PICKUP_DELTA_SWORD";
		
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 1000;
		
		DeltaWeapon.RedCrosshairRange 200;
		DeltaWeapon.MaxLungeDistance 200;
		DeltaWeapon.Crosshair "DRSWDRET";
		
		+WEAPON.AXEBLOOD
		+WEAPON.MELEEWEAPON
		+WEAPON.NOALERT			// wanna handle this manually
		+SQUAREPIXELS
	}
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		pulledOutFirstTime = false;
	}
	
	action void A_KrisSwordSwing()
	{
		invoker.swingCombo++;
		invoker.ResetReadyToStopLunging();
		
		A_StartSound("delta/sword/swing", CHAN_WEAPON, CHANF_OVERLAP, pitch: cfrandom(0.95, 1.05));
		
		int dmg;
		int TRACER_COUNT = 16;
		double distance = 80;
		Array<Actor> targets;
		
		for (double ang = -45; ang <= 45; ang += 45./TRACER_COUNT)
		{
			dmg = random[swordDamage](35, 50);
			FTranslatedLineTarget t;
			FLineTraceData l;
			
			double pitch = AimLineAttack(ang, distance, t, 0., ALF_CHECK3D);
			LineTrace(Angle + ang, distance, pitch, TRF_NOSKY, player.mo.height*0.5 - player.mo.floorclip + player.mo.AttackZOffset*player.crouchFactor, data: l);
			
			if (!l.HitActor)
				continue;
			if (targets.Find(l.HitActor) != targets.Size())
				continue;
			if (l.HitActor is "ExplosiveBarrel")
				continue;
			
			LineAttack(Angle + ang, distance, pitch, dmg, "Melee", "KrisSwordPuff", 0, t);
			targets.Push(l.HitActor);
		}
		
		A_AlertMonsters();
		
		if (targets.Size() > 0)
		{
			Radius_Quake(2, 3, 0, 1, 0);
			A_StartSound("delta/sword/hit", CHAN_WEAPON, CHANF_OVERLAP);
		}
		else
			LineAttack(Angle, distance, Pitch, dmg, "Melee", "KrisSwordPuff");
	}
	
	States
	{
		Ready:
			DKSW A 1 A_DeltaWeaponReady;
			Loop;
		Deselect:
			DKWH A 2;
			#### B 2 A_StartSound("delta/sword/holster", CHAN_WEAPON, CHANF_OVERLAP);
			#### CD 2;
			TNT1 A 4;
			#### # 0 A_SwapWeapon;
			Stop;
		Select:
			TNT1 A 2
			{
				A_StartRaiseWeapon();
				A_StartSound("delta/sword/deploy", CHAN_WEAPON);
			}
			DKWD A 2 A_DeltaWeaponReady(WRF_NOBOB | WRF_NOSWITCH);
			DKWD B 5
			{
				if (!invoker.pulledOutFirstTime)
				{
					A_StartSound("delta/sword/deploy_hit", CHAN_WEAPON, CHANF_OVERLAP);
					invoker.pulledOutFirstTime = true;
				}
			}
			DKWD CD 2;
			DKWD E 2 A_DeltaWeaponReady(WRF_NOBOB);
			DKWD FG 3 A_DeltaWeaponReady(WRF_NOBOB);
			DKWD H 3 A_StartSound("delta/sword/catch", CHAN_WEAPON, CHANF_OVERLAP);
			DKSW A 1 A_FinishRaiseWeapon;
			Stop;
		Fire:
			TNT1 A 0
			{
				if ((invoker.swingCombo + 1) % 2 == 0)
					return ResolveState("LungeL");
				
				return ResolveState("LungeR");
			}
			Goto SwingR;
		LungeL:
			DKSW K 1 A_DoLunge(ResolveState("SwingL"));
			Loop;
		SwingL:
			DKSW L 1 A_KrisSwordSwing;
			DKSW L 1 A_AddRecoil(5, 1, p: 2, y: cl_delta_weaponHandedness ? -5 : 5);
			DKSW M 1;
			DKSW N 4;
			DSKW O 2;
			DKSW PPQQRRSSTTAAA 1 A_ReFire;
			Goto Ready;
		LungeR:
			DKSW B 1 A_DoLunge(ResolveState("SwingR"));
			Loop;
		SwingR:
			DKSW C 1 A_KrisSwordSwing;
			DKSW C 1 A_AddRecoil(5, 1, p: 2, y: cl_delta_weaponHandedness ? 5 : -5);
			DKSW D 1;
			DKSW E 6;
			DKSW FFGGHHIIJJAAA 1 A_ReFire;
			Goto Ready;
	}
}

class KrisSwordPuff : BulletPuff
{
	Default
	{
		ProjectileKickBack 5;
		
		+NOEXTREMEDEATH
	}
}