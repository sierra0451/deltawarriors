class KrisFists : DeltaWeapon
{
	const START_COMBO_TIMER = 10;
	
	int punchCombo;
	int comboTimer;
	
	Default
	{
		Weapon.SlotNumber 1;
		Weapon.SelectionOrder 4500;
		
		Tag "Fists";
		
		+WEAPON.MELEEWEAPON
		+WEAPON.WIMPY_WEAPON
		+WEAPON.NOALERT
		+SQUAREPIXELS
	}
	
	action void A_KrisSwingFists()
	{
		invoker.punchCombo++;
		invoker.comboTimer = START_COMBO_TIMER;
		
		FTranslatedLineTarget t;
		double ang = angle + Random2() * (5.625 / 256);
		double pitch = AimLineAttack(ang, 64, t, 0., ALF_CHECK3D);
		int dmg = 15;
		
		if (t.lineTarget)
		{
			Radius_Quake(2, 3, 0, 1, 0);
			A_StartSound("delta/fists/hit", CHAN_WEAPON, CHANF_OVERLAP);
		}
		
		LineAttack(ang, 64, pitch, dmg, "Melee", "KrisFistsPuff", LAF_ISMELEEATTACK);
	}
	
	action void A_KrisHook()
	{
		invoker.punchCombo = 0;
		invoker.comboTimer = 0;
		
		FTranslatedLineTarget t;
		double ang = angle + Random2() * (5.625 / 256);
		double pitch = AimLineAttack(ang, 64, t, 0., ALF_CHECK3D);
		int dmg = 30;
		
		if (t.lineTarget)
		{
			Radius_Quake(3, 4, 0, 1, 0);
			A_StartSound("delta/fists/hit", CHAN_WEAPON, CHANF_OVERLAP);
		}
		
		LineAttack(ang, 64, pitch, dmg, "Melee", "KrisFistsPuff", LAF_ISMELEEATTACK);
	}
	
	protected void HandleComboTimer()
	{
		if (punchCombo == 0)
			return;
		
		if (comboTimer > 0)
		{
			comboTimer--;
			return;
		}
		
		punchCombo = 0;
	}
	
	States
	{
		Ready:
			DKFS A 1
			{
				invoker.HandleComboTimer();
				A_WeaponReady();
			}
			Loop;
		Deselect:
			DKFS A 1 A_Lower(10);
			Loop;
		Select:
			DKFS A 1 A_Raise(10);
			Loop;
		Fire:
			TNT1 A 0
			{
				if (invoker.punchCombo == 2)
					return ResolveState("Hook");
				else if (invoker.punchCombo == 1)
					return ResolveState("PunchR");
				
				return ResolveState("PunchL");
			}
			Stop;
		PunchL:
			DKFS B 1 A_StartSound("delta/fists/swing", CHAN_WEAPON, CHANF_OVERLAP);
			DKFS C 2 A_KrisSwingFists;
			DKFS DEFG 1;
			DKFS A 2;
			DKFS A 1 A_ReFire;
			Goto Ready;
		PunchR:
			DKFS H 1 A_StartSound("delta/fists/swing", CHAN_WEAPON, CHANF_OVERLAP);
			DKFS I 2 A_KrisSwingFists;
			DKFS JKLM 1;
			DKFS A 2;
			DKFS A 1 A_ReFire;
			Goto Ready;
		Hook:
			DKFS N 1 A_StartSound("delta/fists/hook", CHAN_WEAPON, CHANF_OVERLAP);
			DKFS O 1 A_QuakeEx(0, 3, 0, 7, 0, 1, "dsempty", QF_RELATIVE | QF_WAVE | QF_SCALEDOWN, rollWave: 5);
			DKFS P 1 A_KrisHook;
			DKFS QRS 2;
			DKFS TUVW 2;
			DKFS A 3;
			Goto Ready;
	}
}

class KrisFistsPuff : BulletPuff
{
	Default
	{
		Obituary "$OBIT_KRIS_FISTS";
		ProjectileKickBack 20;
	}
}