class KrisRevolver : DeltaWeapon
{
	bool pulledOutFirstTime;
	bool cocked;
	
	Default
	{
		Tag "$TAG_DELTA_REVOLVER";
		Obituary "$OBIT_DELTA_REVOLVER";
		Inventory.RestrictedTo "KrisPlayer";
		Inventory.PickupMessage "$PICKUP_DELTA_REVOLVER";
		
		Weapon.SlotNumber 2;
		Weapon.SelectionOrder 900;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 15;
		Weapon.AmmoType "Clip";
		DeltaWeapon.Crosshair "DRPSTRET";
		
		+SQUAREPIXELS
	}
	
	override void BeginPlay()
	{
		Super.BeginPlay();
		cocked = true;
	}
	
	protected action void A_KrisRevolverFire()
	{
		A_FireBullets(1, 1, 1, random[revolverDmg](30, 50), "KrisRevolverPuff", FBF_USEAMMO | FBF_NORANDOM);
		A_AlertMonsters();
		A_StartSound("delta/revolver/fire", CHAN_WEAPON, CHANF_OVERLAP);
		A_ZoomFactor(0.97, ZOOM_INSTANT | ZOOM_NOSCALETURNING);
		self.A_AttachLightDef("revolverMuzzle", "DeltaGenericMuzzle");
		invoker.cocked = false;
	}
	
	States
	{
		Ready:
			DKRV A 1
			{
				if (!invoker.cocked)
					return ResolveState("Cock");
				
				return A_DeltaWeaponReady();
			}
			Loop;
		Deselect:
			DKRH A 2 A_StartSound("delta/shotgun/holster", CHAN_WEAPON, CHANF_OVERLAP);
			DKRH BC 2;
			DKRH D 3;
			TNT1 A 4 A_StartSound("delta/revolver/holster", CHAN_WEAPON, CHANF_OVERLAP);
			TNT1 A 0 A_SwapWeapon;
			Stop;
		Select:
			TNT1 A 2 A_StartRaiseWeapon;
			DKRD A 2 A_StartSound("delta/revolver/deploy1", CHAN_WEAPON, CHANF_OVERLAP);
			DKRD C 2;
			DKRD DF 2 A_DeltaWeaponReady(WRF_NOBOB | WRF_NOSWITCH);
			DKRD G 3 A_StartSound("delta/revolver/deploy2", CHAN_WEAPON, CHANF_OVERLAP);
			DKRD H 2 A_DeltaWeaponReady(WRF_NOBOB);
			DKRD IJ 2 A_DeltaWeaponReady(WRF_NOBOB);
			DKRV A 3 A_FinishRaiseWeapon;
			Stop;
		Fire:
			DKRV B 2 A_KrisRevolverFire;
			DKRV C 1 A_RemoveLight("revolverMuzzle");
			DKRV D 2 A_ZoomFactor;
			DKRV E 3;
			DKRV FG 2;
			DKRV H 1;
			DKRV H 1 A_CheckReload; // fall-through is intentional
		Cock:
			DKRV I 2 A_DeltaWeaponReady(WRF_NOFIRE | WRF_NOBOB);
			DKRV J 3 A_StartSound("delta/revolver/cock1", CHAN_WEAPON, CHANF_OVERLAP);
			DKRV K 3;
			//DKRV K 3;
			DKRV L 4 A_StartSound("delta/revolver/cock2", CHAN_WEAPON, CHANF_OVERLAP);
			DKRV N 4
			{
				invoker.cocked = true;
				
			}
			//DKRV N 5;
			DKRV P 3;
			DKRV Q 3 A_ReFire;
			DKRV R 2 A_ReFire;
			Goto Ready;
	}
}

class KrisRevolverPuff : BulletPuff
{
	Default
	{
		ProjectileKickBack 1;
		+NOEXTREMEDEATH
	}
}