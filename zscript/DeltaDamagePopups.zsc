class DeltaDamagePopup : Actor
{
	int timer;
	int scaleUpTime, popUpTime, bounceTime, holdTime, fadeTime;
	double startVelZ, bounceVelZ;
	
	Default
	{
		-SOLID;
		+NOGRAVITY;
		+NOINTERACTION;
		+SQUAREPIXELS;
		+BRIGHT;
		+INVISIBLEINMIRRORS;
		+FORCEXYBILLBOARD;
		+MASTERNOSEE;
	}
	
	States
	{
		Spawn:
			DDAM L -1;
			Stop;
	}
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		scaleUpTime = 3;
		popUpTime = 13;
		bounceTime = 8;
		holdTime = TICRATE;
		fadeTime = floor(TICRATE * 0.5);
		startVelZ = vel.z;
	}
	
	override void Tick()
	{
		Super.Tick();
		timer++;
		int tempTime = timer; // using this to make code easier to debug and read
		
		if (tempTime < popUpTime)
		{
			if (tempTime <= scaleUpTime)
				A_SetScale(4 - 3*((float(tempTime)/scaleUpTime)**2), (float(tempTime) / scaleUpTime)**2);
			
			double reduceAmount = 2. * startVelZ / popUpTime;
			vel.z -= reduceAmount;
			return;
		}
		
		if (tempTime == popUpTime)
			vel = (0, 0, 0);
		
		tempTime -= popUpTime;
		
		if (!tempTime)
		{
			bounceVelZ = startVelZ / 4;
			vel.z = bounceVelZ;
			return;
		}
		else if (tempTime < bounceTime)
		{
			double reduceAmount = 2 * bounceVelZ / bounceTime;
			vel.z -= reduceAmount;
			return;
		}
		else if (tempTime == bounceTime)
		{
			vel.z = 0;
		}
		
		tempTime -= bounceTime;
		
		if (tempTime <= holdTime)
			return;
		
		tempTime -= holdTime;
		
		vel.z = 1;
		A_SetScale(1, 0.9 + (tempTime)*2./TICRATE);
		A_FadeOut(1. / fadeTime);
	}
}

class DeltaDamageNumber : DeltaDamagePopup
{
	int place, value, totalDigits;
	
	override void PostBeginPlay()
	{
		Super.PostBeginPlay();
		
		TextureID texID;
		texID = curState.GetSpriteTexture(0);
		int width = TexMan.GetSize(texID);
		
		double xOffset = (width+1) * (-(totalDigits/2) + (place-1));
		A_SpriteOffset(xOffset, 0);
	}
	
	static void SpawnNumbers(Actor thing, int dmg, Actor source)
	{
		string damageStr = "" .. dmg;
		
		let posDiff = source ? level.Vec3Diff(thing.pos, source.pos) : (0, 0, 0);
		posDiff.z = 0;
		posDiff = posDiff.Unit();
		let totalDigits = damageStr.Length()-1,
			numVel = (posDiff.x * thing.radius/20., posDiff.y * thing.radius/20.,
				cfrandom(5,7)),
			numLifeTime = crandom(-3, 3);
		let trans = source ? source.GetClassName() .. "_damage" : "";
		let position = thing.pos
			+ (cfrandom(-3, 3),cfrandom(-3, 3),0)
			+ (posDiff * (thing.radius+5));
		
		let targetZ = thing.height/2. + thing.pos.z;
		
		if (thing.health <= 0)
			targetZ = source ? (-thing.height/2. + source.height + thing.pos.z) : (thing.height/2. + thing.pos.z);
		
		position.z = targetZ + cfrandom(-3, 3);
		
		for (uint i = 0; i <= totalDigits; i++)
		{
			let num = DeltaDamageNumber(Spawn("DeltaDamageNumber",
				position));
			
// 			if (!i)
// 				num.A_Log(trans);
			
			num.vel = numVel;
			num.place = i;
			num.totalDigits = totalDigits;
			num.value = damageStr.ByteAt(i) - 48;
			num.A_SetTranslation(trans);
			num.master = thing;
		}
	}
	
	action state A_SetNumber()
	{
		switch (invoker.value)
		{
			case -3:
				return ResolveState("Minus");
			case 0:
				return ResolveState("Digit0");
			case 1:
				return ResolveState("Digit1");
			case 2:
				return ResolveState("Digit2");
			case 3:
				return ResolveState("Digit3");
			case 4:
				return ResolveState("Digit4");
			case 5:
				return ResolveState("Digit5");
			case 6:
				return ResolveState("Digit6");
			case 7:
				return ResolveState("Digit7");
			case 8:
				return ResolveState("Digit8");
			case 9:
				return ResolveState("Digit9");
			default:
				return ResolveState(null);
		}
	}
	
	States
	{
		Spawn:
			DDAM A 0;
			DDAM # 1 A_SetNumber;
			Wait;
		Minus:
			DDAM A -1;
			Stop;
		Plus:
			DDAM B -1;
			Stop;
		Digit0:
			DDAM C -1;
			Stop;
		Digit1:
			DDAM D -1;
			Stop;
		Digit2:
			DDAM E -1;
			Stop;
		Digit3:
			DDAM F -1;
			Stop;
		Digit4:
			DDAM G -1;
			Stop;
		Digit5:
			DDAM H -1;
			Stop;
		Digit6:
			DDAM I -1;
			Stop;
		Digit7:
			DDAM J -1;
			Stop;
		Digit8:
			DDAM K -1;
			Stop;
		Digit9:
			DDAM L -1;
			Stop;
	}
}