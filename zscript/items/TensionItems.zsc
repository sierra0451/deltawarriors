/// base class for tension items!
class DeltaTensionPickup : Inventory
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 250;
		+INVENTORY.KEEPDEPLETED
	}
	
	override bool TryPickup(in out Actor toucher)
	{
		let delta = DeltaPlayer(toucher);
		
		if (!delta)
			return false;
		if (delta.tensionPoints >= delta.maxTensionPoints && !bAlwaysPickup)
			return false;
		if (!Super.TryPickup(toucher))
			return false;
		
		delta.AddTension(Amount);
		GoAwayAndDie();
		return true;
	}
}

class DeltaTensionBit : DeltaTensionPickup replaces ArmorBonus
{
	Default
	{
		Tag "$TAG_DELTA_TPSMALL";
		Inventory.Amount 20;
		Inventory.PickupMessage "$PICKUP_DELTA_TPSMALL";
		
		+COUNTITEM
		+INVENTORY.ALWAYSPICKUP
	}
	
	States
	{
		Spawn:
			DTNB ABCB 8;
			Loop;
	}
}

class DeltaTensionGem : DeltaTensionPickup replaces GreenArmor
{
	Default
	{
		Tag "$TAG_DELTA_TPMED";
		Inventory.Amount 125;
		Inventory.PickupMessage "$PICKUP_DELTA_TPMED";
	}
	
	States
	{
		Spawn:
			DTNG ABCB 8;
			Loop;
	}
}

class DeltaTensionMax : DeltaTensionPickup replaces BlueArmor
{
	Default
	{
		Tag "$TAG_DELTA_TPLARGE";
		Inventory.Amount 250;
		Inventory.PickupMessage "$PICKUP_DELTA_TPLARGE";
	}
	
	States
	{
		Spawn:
			DTNM ABCB 8;
			Loop;
	}
}