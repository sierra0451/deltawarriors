struct GrazeEvent play version("2.4")
{
	DeltaPlayer thing;
	Actor grazeSource;
}

class DeltaWarriorsEvents : EventHandler
{
	override void WorldThingDamaged(WorldEvent e)
	{
		EnableNetworking(true);
		
		if (!e.thing || e.damage <= 0)
			return;
		
		if (e.damageSource is "DeltaPlayer" && e.damageSource != e.thing)
		{
			DeltaPlayer ply = DeltaPlayer(e.damageSource);
			
			if (!ply.GetDamageTics())
				ply.AddTension(e.damage / 8);
		}
		
		if (e.thing is "DeltaPlayer" && e.thing != e.damageSource
			&& DeltaPlayer(e.thing).GetDamageTics() == 0)
		{
			EventHandler.SendInterfaceEvent(e.thing.PlayerNumber(), "DeltaPlayerDamage",
				e.damageSource ? e.damageSource.GetNetworkID() : 0);
		}
	}
	
	override void PlayerEntered(PlayerEvent e)
	{
		EventHandler.SendInterfaceEvent(e.playerNumber, "ResetTP");
	}
	
	override void PlayerRespawned(PlayerEvent e)
	{
		EventHandler.SendInterfaceEvent(e.playerNumber, "ResetTP");
	}
	
	virtual void PlayerGrazed(GrazeEvent e)
	{
		EnableNetworking(true);
		
		if (e.thing != e.grazeSource)
		{
			EventHandler.SendInterfaceEvent(e.thing.PlayerNumber(), "DeltaPlayerGraze",
				e.grazeSource ? e.grazeSource.GetNetworkID() : 0);
		}
	}
	
	override void InterfaceProcess(ConsoleEvent e)
	{
		if (e.IsManual) return;
		
		if (e.name ~== "ResetTP")
		{
			if (!(statusBar is "DeltaHud"))
				return;
			
			let hud = DeltaHud(statusBar);
			hud._tpInterp.Reset(0);
		}
		else if (e.name ~== "DeltaPlayerDamage")
		{
			if (!(statusBar is "DeltaHud"))
				return;
			
			let hud = DeltaHud(statusBar);
			hud.CreateDamageIndicator(Actor(GetNetworkEntity(e.Args[0])));
		}
		else if (e.name ~== "DeltaPlayerGraze")
		{
			if (!(statusBar is "DeltaHud"))
				return;
			
			let hud = DeltaHud(statusBar);
			hud.CreateGrazeIndicator(Actor(GetNetworkEntity(e.Args[0])));
		}
	}
}